# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/features.gni")
import(
    "//chrome/browser/resources/chromeos/accessibility/tools/run_jsbundler.gni")
import("//chrome/common/features.gni")
import("//chrome/test/base/ash/js2gtest.gni")
import("//testing/test.gni")

assert(is_chromeos_ash)

switch_access_dir =
    "$root_out_dir/resources/chromeos/accessibility/switch_access"

group("build") {
  deps = [ ":switch_access_copied_files" ]
}

# Instead of setting up one copy target for each subdirectory, use a script
# to copy all files.
run_jsbundler("switch_access_copied_files") {
  mode = "copy"
  dest_dir = switch_access_dir
  sources = [
    "action_manager.js",
    "auto_scan_manager.js",
    "background.html",
    "cache.js",
    "commands.js",
    "focus_ring_manager.js",
    "history.js",
    "icons/back.svg",
    "icons/copy.svg",
    "icons/cut.svg",
    "icons/decrement.svg",
    "icons/dictation.svg",
    "icons/increment.svg",
    "icons/jumpToBeginningOfText.svg",
    "icons/jumpToEndOfText.svg",
    "icons/keyboard.svg",
    "icons/moveBackwardOneCharOfText.svg",
    "icons/moveBackwardOneWordOfText.svg",
    "icons/moveCursor.svg",
    "icons/moveDownOneLineOfText.svg",
    "icons/moveForwardOneCharOfText.svg",
    "icons/moveForwardOneWordOfText.svg",
    "icons/moveUpOneLineOfText.svg",
    "icons/paste.svg",
    "icons/scrollDownOrForward.svg",
    "icons/scrollLeft.svg",
    "icons/scrollRight.svg",
    "icons/scrollUpOrBackward.svg",
    "icons/select.svg",
    "icons/settings.svg",
    "icons/showContextMenu.svg",
    "icons/textSelectionEnd.svg",
    "icons/textSelectionStart.svg",
    "item_scan_manager.js",
    "menu_manager.js",
    "metrics.js",
    "navigator.js",
    "navigator_interfaces.js",
    "nodes/back_button_node.js",
    "nodes/basic_node.js",
    "nodes/combo_box_node.js",
    "nodes/desktop_node.js",
    "nodes/editable_text_node.js",
    "nodes/group_node.js",
    "nodes/keyboard_node.js",
    "nodes/modal_dialog_node.js",
    "nodes/slider_node.js",
    "nodes/switch_access_node.js",
    "nodes/tab_node.js",
    "nodes/window_node.js",
    "point_scan_manager.js",
    "settings_manager.js",
    "switch_access.js",
    "switch_access_constants.js",
    "switch_access_predicate.js",
    "text_navigation_manager.js",
  ]
  rewrite_rules = [
    rebase_path(".", root_build_dir) + ":",
    rebase_path(closure_library_dir, root_build_dir) + ":closure",
  ]
}

source_set("browser_tests") {
  testonly = true
  assert(enable_extensions)

  deps = [ ":switch_access_extjs_tests" ]

  data = [
    "//chrome/browser/resources/chromeos/accessibility/common/",
    "//chrome/browser/resources/chromeos/accessibility/switch_access/",
  ]
  data += js2gtest_js_libraries
}

js2gtest("switch_access_extjs_tests") {
  test_type = "extension"
  sources = [
    "auto_scan_manager_test.js",
    "focus_ring_manager_test.js",
    "item_scan_manager_test.js",
    "nodes/basic_node_test.js",
    "nodes/desktop_node_test.js",
    "nodes/group_node_test.js",
    "nodes/tab_node_test.js",
    "point_scan_manager_test.js",

    # TODO(1316408): Tests were broken at head. Re-enable tests when they pass.
    # "saatlite/gen/saatlite_tests.js",
    "switch_access_predicate_test.js",
    "switch_access_test.js",
    "text_navigation_manager_test.js",
  ]
  gen_include_files = [
    "../common/testing/accessibility_test_base.js",
    "../common/testing/assert_additions.js",
    "../common/testing/callback_helper.js",
    "../common/testing/e2e_test_base.js",
    "switch_access_e2e_test_base.js",
    "test_utility.js",
  ]

  # The test base classes generate C++ code with these deps.
  deps = [
    "//ash",
    "//ash/keyboard/ui",
    "//base",
    "//chrome/browser/ash",
    "//chrome/browser/ash/crosapi",
    "//chrome/common",
    "//chromeos",
  ]
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
}

group("closure_compile") {
}
