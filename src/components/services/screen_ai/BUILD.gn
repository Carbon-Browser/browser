# Copyright 2022 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/protobuf/proto_library.gni")

source_set("screen_ai") {
  sources = [
    "screen_ai_service_impl.cc",
    "screen_ai_service_impl.h",
  ]

  deps = [
    "//base",
    "//components/services/screen_ai/proto:proto_convertors",
    "//components/services/screen_ai/public/cpp:utilities",
    "//components/services/screen_ai/public/mojom",
    "//mojo/public/cpp/bindings",
    "//sandbox/policy",
    "//ui/accessibility:ax_base",
  ]
}

if (!is_mac) {
  source_set("screen_ai_sandbox_hook") {
    sources = [
      "sandbox/screen_ai_sandbox_hook_linux.cc",
      "sandbox/screen_ai_sandbox_hook_linux.h",
    ]

    deps = [
      "//base",
      "//components/component_updater",
      "//components/services/screen_ai/public/cpp:utilities",
      "//sandbox/linux:sandbox_services",
      "//ui/accessibility:ax_base",
    ]

    public_deps = [ "//sandbox/policy" ]
  }
}

group("unit_tests_data") {
  visibility = [ ":unit_tests" ]
  testonly = true
  data = [
    "$root_gen_dir/components/services/screen_ai/proto/view_hierarchy.descriptor",
    "//components/test/data/screen_ai/sample_01_ax_tree.json",
    "//components/test/data/screen_ai/sample_01_expected_proto.pbtxt",
  ]
  deps = [
    "//components/services/screen_ai/proto:view_hierarchy_proto_descriptor",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "proto/proto_convertor_unittest.cc",
    "proto/test_proto_loader.cc",
    "proto/test_proto_loader.h",
  ]

  data_deps = [ ":unit_tests_data" ]

  deps = [
    "//components/services/screen_ai/proto",
    "//components/services/screen_ai/proto:proto_convertors",
    "//testing/gtest",
    "//third_party/protobuf:protobuf_full",
    "//ui/accessibility:test_support",
  ]
}
