# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")

assert(is_chromeos, "Non-Chrome-OS builds must not depend on //chromeos")

component("dbus") {
  output_name = "chromeos_dbus"  # Avoid conflict with //dbus
  defines = [ "IS_CHROMEOS_DBUS_IMPL" ]
  public_deps = [
    "//chromeos/dbus/common",
    "//chromeos/dbus/constants",

    # TODO(jamescook): Remove this and fix all build targets that need it.
    "//chromeos/dbus/debug_daemon",
    "//chromeos/dbus/init",
    "//chromeos/dbus/shill",
    "//dbus",
  ]
  deps = [
    "//base",
    "//chromeos/dbus/util",
  ]
  sources = [
    "dbus_thread_manager.cc",
    "dbus_thread_manager.h",
  ]
}

source_set("test_support") {
  testonly = true
  configs += [ "//build/config/linux/dbus" ]
  public_deps = [
    ":dbus",
    "//chromeos/dbus/constants",
    "//dbus",
    "//dbus:test_support",
  ]
  deps = [
    "//base",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

source_set("unit_tests") {
  configs += [ "//build/config/linux/dbus" ]
  testonly = true
  deps = [
    ":dbus",
    ":test_support",
    "//base",
    "//base/test:test_support",
    "//chromeos/dbus/cryptohome",
    "//chromeos/dbus/cryptohome:attestation_proto",
    "//chromeos/dbus/dlcservice:unit_tests",
    "//chromeos/dbus/easy_unlock:unit_tests",
    "//chromeos/dbus/fwupd:test_support",
    "//chromeos/dbus/power:power_manager_proto",
    "//chromeos/dbus/power:test_support",
    "//chromeos/dbus/shill:test_support",
    "//chromeos/dbus/tpm_manager",
    "//chromeos/dbus/tpm_manager:tpm_manager_proto",
    "//chromeos/dbus/u2f",
    "//chromeos/dbus/u2f:u2f_proto",
    "//chromeos/dbus/util:unit_tests",
    "//dbus",
    "//dbus:test_support",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/icu",
  ]
  sources = [
    "blocking_method_caller_unittest.cc",
    "dbus_thread_manager_unittest.cc",
    "native_timer_unittest.cc",
    "pipe_reader_unittest.cc",
    "tpm_manager/tpm_manager_client_unittest.cc",
  ]
}
